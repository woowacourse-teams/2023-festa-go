plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.4'
    id 'io.spring.dependency-management' version '1.1.0'
}

group = 'com'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

repositories {
    mavenCentral()
}

springBoot {
    buildInfo()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.2'

    // Spring Security
    implementation 'org.springframework.security:spring-security-crypto'

    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'com.mysql:mysql-connector-j'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.rest-assured:rest-assured:5.3.0'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    // Logback Slack Alarm
    implementation "com.github.maricn:logback-slack-appender:1.4.0"

    // Cucumber
    testImplementation 'io.cucumber:cucumber-java:7.13.0'
    testImplementation 'io.cucumber:cucumber-spring:7.13.0'
    testImplementation 'io.cucumber:cucumber-junit-platform-engine:7.13.0'
    testImplementation 'org.junit.platform:junit-platform-suite:1.8.2'

    // Querydsl
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    // Flyway
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-mysql'

    // Firebase
    implementation 'com.google.firebase:firebase-admin:8.1.0'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
}

tasks.named('test') {
    useJUnitPlatform()
}

// Querydsl 폴더 지정
def querydslDir = file('build/generated/querydsl')

// querydsl QClass 파일 생성 위치를 지정
compileJava {
    options.getGeneratedSourceOutputDirectory().set(querydslDir)
}

// java source set 에 querydsl QClass 위치 추가
sourceSets {
    main.java.srcDirs.add(querydslDir)
}
